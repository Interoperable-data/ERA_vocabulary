



PREFIX era: <http://data.europa.eu/949/>

SELECT DISTINCT ?p ?inCountry ?numTotalTracks ?numTracksWithPropertyAsCoreParameter ?numTracksWithoutPropertyAsCoreParameter
WHERE {
GRAPH <${{NAMED_KG_RINF}}> {
VALUES ?p {era:temperatureRange era:trackLoadCapability era:maximumPermittedSpeed era:hasSevereWeatherConditions era:gaugingProfile era:gradientProfile era:minimumHorizontalRadius era:wheelSetGauge era:cantDeficiency era:railInclination era:minimumWheelDiameter era:maximumTrainDeceleration era:eddyCurrentBraking era:magneticBraking era:hasHotAxleBoxDetector era:rollingStockFireCategory era:contactLineSystemType era:energySupplySystem}
# To be added to other things that are not tracks: era:protectionLegacySystem era:legacyRadioSystem era:trainDetectionSystemType era:maximumBrakingDistance era:hasAdditionalBrakingInformation era:lineReference era:gaugingProfile era:length era:platformHeight}
#missing (not in the ontology)  era:geographicalLocationOperationalPoint (1.2.0.0.0.5), 
# Check two appearances of the gaugingProfile (1.1.1.1.3.1.1 and 1.2.1.0.3.4), wheelSetGauge, 
{
SELECT DISTINCT ?inCountry (COUNT(DISTINCT ?track) AS ?numTotalTracks)
WHERE {
  ?track a era:Track .  
  ?sectionOfLine era:track ?track .
  ?sectionOfLine a era:SectionOfLine .
  ?sectionOfLine era:inCountry ?inCountry
} 
} OPTIONAL
{
SELECT DISTINCT ?inCountry ?p (COUNT(DISTINCT ?track) AS ?numTracksWithPropertyAsCoreParameter)
WHERE {
VALUES ?p {era:temperatureRange era:trackLoadCapability era:maximumPermittedSpeed era:hasSevereWeatherConditions era:gaugingProfile era:gradientProfile era:minimumHorizontalRadius era:wheelSetGauge era:cantDeficiency era:railInclination era:minimumWheelDiameter era:maximumTrainDeceleration era:eddyCurrentBraking era:magneticBraking era:hasHotAxleBoxDetector era:rollingStockFireCategory era:contactLineSystemType era:energySupplySystem}
  ?track a era:Track .  
  ?track ?p ?propertyValue .
  ?sectionOfLine era:track ?track .
  ?sectionOfLine a era:SectionOfLine .
  ?sectionOfLine era:inCountry ?inCountry.
} 
} OPTIONAL
{
SELECT DISTINCT ?inCountry ?p (COUNT(DISTINCT ?track) AS ?numTracksWithoutPropertyAsCoreParameter) 
WHERE {
VALUES ?p {era:temperatureRange era:trackLoadCapability era:maximumPermittedSpeed era:hasSevereWeatherConditions era:gaugingProfile era:gradientProfile era:minimumHorizontalRadius era:wheelSetGauge era:cantDeficiency era:railInclination era:minimumWheelDiameter era:maximumTrainDeceleration era:eddyCurrentBraking era:magneticBraking era:hasHotAxleBoxDetector era:rollingStockFireCategory era:contactLineSystemType era:energySupplySystem}
  ?track a era:Track .  
  ?sectionOfLine era:track ?track .
  ?sectionOfLine a era:SectionOfLine .
  ?sectionOfLine era:inCountry ?inCountry.
  FILTER NOT EXISTS {?track ?p ?propertyValue .}
} 
}
}
} ORDER BY ?p ?inCountry 
