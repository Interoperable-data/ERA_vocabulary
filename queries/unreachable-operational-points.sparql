PREFIX era: <http://data.europa.eu/949/>
PREFIX era-dir: <http://data.europa.eu/949/concepts/track-running-directions/rinf/>

SELECT DISTINCT ?op ?uopid ?country WHERE {
GRAPH <${{NAMED_KG_RINF}}> {
    # Main OP to be analized
    ?op era:inCountry ?country;
        era:uopid ?uopid.
        
    # Main SoL to be analized with tracks only departing from the main OP
    ?sol a era:SectionOfLine;
         era:track [ era:trackDirection era-dir:10 ];
         era:opStart ?op.
	
    # Condition: There are no other tracks in the main SoL with arriving directions
    FILTER NOT EXISTS {
        ?sol era:track [ era:trackDirection era-dir:20 ]
    }
    FILTER NOT EXISTS {
        ?sol era:track [ era:trackDirection era-dir:30 ]
    }
    # Condition: There are no SoLs also departing on the main OP with tracks having arriving directions
    FILTER NOT EXISTS {
        ?sol1 era:opStart ?op;
              era:track [ era:trackDirection era-dir:20 ]
    }
    FILTER NOT EXISTS {
        ?sol1 era:opStart ?op;
              era:track [ era:trackDirection era-dir:30 ]
    }
    # Condition: There are no other SoLs arriving on the main OP with tracks having arriving directions
    FILTER NOT EXISTS {
        ?sol2 era:opEnd ?op;
              era:track [ era:trackDirection era-dir:10 ]
    }
    FILTER NOT EXISTS {
        ?sol2 era:opEnd ?op;
              era:track [ era:trackDirection era-dir:30 ]
    }
}
}
ORDER BY ?op ?country
